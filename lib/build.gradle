plugins {
    id 'java'
    id("io.freefair.lombok") version "8.12.2.1"
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2025.+'
    id 'maven-publish'
    id("net.ltgt.errorprone") version "4.2.0"
}

group = 'team1306'

repositories {
    mavenCentral()
    maven {
        url "https://maven.ctr-electronics.com/release/"
    }
    maven {
        url "https://frcmaven.wpi.edu/artifactory/littletonrobotics-mvn-release/"
    }
}

if (project.hasProperty('releaseMode')) {
    wpilibRepositories.addAllReleaseRepositories(project)
} else {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

dependencies {
    compileOnly 'edu.wpi.first.ntcore:ntcore-java:2026.+'

    compileOnly 'edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:2026.+'
    compileOnly 'edu.wpi.first.wpiunits:wpiunits-java:2026.+'
    compileOnly 'edu.wpi.first.wpimath:wpimath-java:2026.+'
    compileOnly 'edu.wpi.first.wpilibj:wpilibj-java:2026.+'
    compileOnly 'edu.wpi.first.wpiutil:wpiutil-java:2026.+'
    compileOnly 'edu.wpi.first.hal:hal-java:2026.+'

    compileOnly "com.ctre.phoenix6:wpiapi-java:26.1.0"

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.2'

    compileOnly 'com.google.auto.service:auto-service:1.0-rc5'
    annotationProcessor 'com.google.auto.service:auto-service:1.0-rc5'

    errorprone("com.google.errorprone:error_prone_core:2.37.0")
    
    implementation "org.littletonrobotics.akit:akit-java:26.0.0"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = "com.github.team1306"
            artifactId = "badger-log"
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:processing'
}

tasks.withType(Javadoc) {
    options {
        docTitle = "Badger Utils Javadoc"
    }
}


javadoc {
    exclude 'com.fasterxml.jackson.annotation.JsonAutoDetect'
}

test {
    useJUnitPlatform()
}

apply plugin: 'groovy'